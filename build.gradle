plugins {
    id 'conventions.spotless'
    id 'org.owasp.dependencycheck'
    id 'com.netflix.nebula.release'
    id 'com.netflix.nebula.maven-nebula-publish' apply false
    id 'com.netflix.nebula.contacts' apply false
    id 'com.netflix.nebula.info' apply false
}

subprojects {
    apply plugin: 'com.netflix.nebula.maven-nebula-publish'
    apply plugin: 'com.netflix.nebula.contacts'
    apply plugin: 'com.netflix.nebula.info'

    contacts {
        'ockenfels.fabian@gmail.com' {
            moniker 'Fabian Ockenfels'
            github 'sbstnc'
        }
    }

    publishing {
        publications {
            nebula(MavenPublication) {
                // remove <dependencyManagement> block from pom
                pom.withXml {
                    asNode().dependencyManagement.each { it.replaceNode {} }
                }
                pom {
                    organization {
                        name = 'sbstnc'
                        url = 'https://github.com/sbstnc'
                    }
                }
            }
        }
    }
}

dependencyCheck {
    failBuildOnCVSS = 5
    analyzers {
        assemblyEnabled = false
        centralEnabled = false
    }
    skipConfigurations = ['spotbugs']
}
