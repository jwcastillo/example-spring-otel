plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'conventions.java'
    id 'com.google.cloud.tools.jib' version '3.2.1'
}

configurations {
    agent
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    agent 'io.opentelemetry.javaagent:opentelemetry-javaagent:1.14.0'
    implementation libs.opentelemetry.api
    implementation libs.opentelemetry.annotations

    implementation project(':jooq-models')
    implementation project(':migrations')

    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.liquibase:liquibase-core'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    testImplementation "org.spockframework:spock-spring"
    testImplementation testLibs.spock.core
    testImplementation testLibs.spock.spring
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.17.1"
    }
}

task downloadAgent(type: Copy) {
    from configurations.agent
    rename('(.*)-.*(.jar)', '$1$2')
    into 'agent/'
}

tasks.processResources.finalizedBy tasks.downloadAgent

jib {
    from {
        image = 'gcr.io/distroless/java:11@sha256:293896be60077932ba49824f0a997371244edac41dcb01ea7b8eac91da7b4be7'
    }
    to {
        image = 'sbstnc/example-spring-otel'
    }
    container {
        ports = ['8080']
        jvmFlags = [
            '-XX:InitialRAMPercentage=80.0',
            '-XX:MaxRAMPercentage=80.0',
            '-Dotel.metrics.exporter=none',
            "-Dotel.resource.attributes=service.name=${rootProject.name}".toString(),
            '-Dotel.javaagent.debug=false',
            '-javaagent:/app/agent/opentelemetry-javaagent.jar',
        ]
    }
    extraDirectories {
        paths {
            path {
                from = 'agent'
                into = '/app/agent'
                permissions = ['*.jar': '644']
            }
        }
    }
}
